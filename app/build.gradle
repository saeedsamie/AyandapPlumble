/*
 * Copyright (C) 2014 Andrew Comminos
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
    }
}
apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://maven.google.com"
    }
}

dependencies {
    compile project(":libraries:Jumble")
    compile 'com.android.support:support-v4:27.1.1'
    compile 'com.android.support:appcompat-v7:27.1.1'
    compile 'com.android.support:cardview-v7:27.1.1'
    compile 'com.android.support:palette-v7:27.1.1'
    compile 'com.android.support:recyclerview-v7:27.1.1'
    compile 'org.jsoup:jsoup:1.7.2'
    compile 'info.guardianproject.netcipher:netcipher:1.2'
    compile 'com.android.support:design:27.1.1'
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:multidex:1.0.0'
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'
    compile 'com.miguelcatalan:materialsearchview:1.4.0'
    compile 'com.wdullaer:swipeactionadapter:2.0.0'
    compile 'de.hdodenhof:circleimageview:2.2.0'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'
}

// Keep my credentials private :^)
def signingFile = file 'signing.gradle';
if (signingFile.exists()) apply from: 'signing.gradle'

android {
    useLibrary 'org.apache.http.legacy'
    compileSdkVersion 28
    buildToolsVersion "28.0.2"

    defaultConfig {
        generatedDensities = []
        aaptOptions {
            additionalParameters "--no-version-vectors"
        }
        multiDexEnabled true
        minSdkVersion 14
        targetSdkVersion 21
        applicationId "com.morlunk.mumbleclient"
        versionCode 73
        versionName "1.0"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        buildConfigField "boolean", "DONATE_NAG", "false"

    }

    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'armeabi-v7a'
            universalApk true
        }
    }
    productFlavors {

        official {
            applicationId "com.morlunk.mumbleclient"
        }

        free {
            applicationId "com.morlunk.mumbleclient.free"
            buildConfigField "boolean", "DONATE_NAG", "true"
        }

        jenkins {
            // Abuse Jenkins environment variables. Neat.
            def env = System.getenv()
            if (env.containsKey("BUILD_NUMBER") && env.containsKey("BUILD_DISPLAY_NAME")) {
                versionCode Integer.parseInt(env.get("BUILD_NUMBER"))
                versionName env.get("BUILD_DISPLAY_NAME")
            } else {
                versionCode 0
                versionName "Unknown Nightly"
            }
            applicationId "com.morlunk.mumbleclient.jenkins"
        }
    }

    lintOptions {
        abortOnError false
    }
}
